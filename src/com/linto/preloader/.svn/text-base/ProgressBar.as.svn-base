package com.aol.pinball.preloader {
	
    import flash.display.*;
    import flash.events.Event;
    import flash.utils.getTimer;
    import flash.text.*;	
    
    /**
     * ProgressBar for Preloader
     */
    public class ProgressBar extends MovieClip {
        
        
		public var display_txt:TextField;
		public var bar_mc:MovieClip;
		public var border:MovieClip;
		public var background:MovieClip;
		
		var _start:int;
		var _delay:int = 750;
		var _offset:Number = 0;
		var _fullwidth:Number = 70;
		
        public function ProgressBar() {
			x = 0;
			y = 0;
			stop();
			reset();
			addEventListener( Event.ADDED_TO_STAGE, init );
        }
        
        function init(e:Event=null):void {			
			stage.scaleMode = StageScaleMode.NO_SCALE;
			stage.align = StageAlign.TOP_LEFT;	
		}
		
		public function reset():void {
			visible = false;
			_start = getTimer();
			_offset = 0;
			bar_mc.width = 0;
			setText('');
		}

		public function setDelay(d:Number):void {
			_delay = d;		
		}
		
		public function setPercent( pct:Number ):void {
			
			// make sure percent is a float
			if(pct>1) pct/=100;	
			
			
			var percentage:Number = Math.ceil(pct*100);
			setText('Loading '+percentage+'%');
			
			if(pct<0) return;
			// wait for delay before displaying
			if((getTimer()-_start) < _delay) {
				return;
			}
		
			if(!visible) {
				_offset = pct;
				visible = true;
			}
			pct = (pct-_offset) / (1-_offset);
			
			bar_mc.width = Math.round( _fullwidth * pct);
			
		}	
		
		public function setText(s:String):void{
			//display_txt.text = s;
		}
		
		public function hide():void {
			visible = false;
		}	
    }
}
